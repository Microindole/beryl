// lencyc/syntax/lexer.lcy
// Lency 自举编译器 - 极简 Lexer
// 目前仅扫描基本的加减乘除和数字，遇到无法识别的字符报 T_ERROR。

import std.core
import std.char
import lencyc.syntax.token

struct Lexer {
    string source
    int start
    int current
    int line
    int column
}

Lexer make_lexer(string source) {
    return Lexer {
        source: source,
        start: 0,
        current: 0,
        line: 1,
        column: 1
    }
}

impl Lexer {
    bool is_at_end() {
        return this.current >= len(this.source);
    }

    string advance() {
        if this.is_at_end() {
            return " ";
        }
        var c = substr(this.source, this.current, 1);
        this.current = this.current + 1;
        this.column = this.column + 1;
        return c;
    }

    string peek() {
        if this.is_at_end() {
            return " ";
        }
        return substr(this.source, this.current, 1);
    }
    
    int peek_char() {
        if this.is_at_end() {
            return 0; // null char
        }
        return this.source[this.current];
    }
    
    // 生成常规 Token
    Token make_token_from(int kind) {
        var l = this.current - this.start;
        var lexeme = substr(this.source, this.start, l);
        // 注意：计算起始列时要扣除当前已经消耗的长度
        return make_token(kind, lexeme, this.line, this.column - l);
    }
    
    // 生成错误 Token
    Token error_token(string message) {
        return make_token(T_ERROR(), message, this.line, this.column);
    }
    
    // 跳过空白字符
    void skip_whitespace() {
        while !this.is_at_end() {
            var c = this.peek();
            if c == " " || c == "\r" || c == "\t" {
                var dummy = this.advance();
            } else {
                if c == "\n" {
                    this.line = this.line + 1;
                    this.column = 1;
                    var dummy = this.advance();
                } else {
                    return;
                }
            }
        }
    }
    
    // 解析标识符和关键字
    Token identifier() {
        while is_alphanumeric(this.peek_char()) || this.peek_char() == 95 { // 95 is '_'
            var dummy = this.advance();
        }
        
        var l = this.current - this.start;
        var text = substr(this.source, this.start, l);
        
        var kind = T_IDENTIFIER();
        if text == "var" { kind = T_VAR(); }
        else { if text == "true" { kind = T_TRUE(); }
        else { if text == "false" { kind = T_FALSE(); } } }
        
        return this.make_token_from(kind);
    }
    
    // 解析数字
    Token number() {
        while is_digit(this.peek_char()) {
            var dummy = this.advance();
        }
        
        // TODO: 支持小数点（浮点数）解析
        
        return this.make_token_from(T_NUMBER());
    }

    // 获取下一个 Token
    Token scan_token() {
        this.skip_whitespace();
        
        this.start = this.current;
        
        if this.is_at_end() {
            return make_token(T_EOF(), "", this.line, this.column);
        }
        
        var c = this.advance();
        
        // 简单符号
        if c == "+" { return this.make_token_from(T_PLUS()); }
        if c == "-" { return this.make_token_from(T_MINUS()); }
        if c == "*" { return this.make_token_from(T_STAR()); }
        if c == "/" { return this.make_token_from(T_SLASH()); }
        
        // 数字字面量
        var c_int = this.source[this.start];
        if is_digit(c_int) {
            return this.number();
        }
        
        if c == "=" { return this.make_token_from(T_EQUAL()); }
        
        // 标识符或关键字
        var is_alpha_char = is_alpha(c_int) || c_int == 95; // 95 is '_'
        if is_alpha_char {
            return this.identifier();
        }
        
        // TODO: 支持双字符操作符 (如 ==, !=, >=, <=)
        // TODO: 支持字符串(Strings)解析
        
        return this.error_token("Unexpected character.");
    }
}
